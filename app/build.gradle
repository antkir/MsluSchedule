apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "by.ntnk.msluschedule"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    androidsupportVersion = '27.1.0'
    androidsupportTestVersion = '1.0.1'
    constraintlayoutVersion = '1.0.2'
    daggerVersion = '2.15'
    espressoVersion = '3.0.1'
    jsoupVersion = '1.11.2'
    junitVersion = '4.12'
    mockitoVersion = '2.15.0'
    okhttpVersion = '3.10.0'
    retrofitVersion = '2.4.0'
    roomVersion = '1.1.0-alpha3'
    rxandroidVersion = '2.0.2'
    rxjavaVersion = '2.1.10'
    threetenbpVersion = '1.3.6'
    timberVersion = '4.6.1'
}

dependencies {
    // Kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    // Support libraries
    implementation "com.android.support:appcompat-v7:$androidsupportVersion"
    implementation "com.android.support:design:$androidsupportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintlayoutVersion"
    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    // Jsoup
    implementation "org.jsoup:jsoup:$jsoupVersion"
    // Okhttp
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    //Room
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"
    // RxAndroid
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    // Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"
    // Time
    implementation "org.threeten:threetenbp:$threetenbpVersion:no-tzdb"
    // Testing
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "com.android.support.test:runner:$androidsupportTestVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
}
